-- programs ------------------------------------------------

entrypoints Program ;

Prog.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= LatteType Ident "(" [Arg] ")" Block ;

separator nonempty TopDef "" ;

Ar. 	   Arg ::= LatteType Ident;

separator  Arg "," ;

-- statements ----------------------------------------------

Blk.       Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= LatteType [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

ArrAss.    Stmt ::= Ident "[" Expr "]" "=" Expr  ";" ;

Incr.      Stmt ::= Ident "++"  ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

WhileStmnt.     Stmt ::= "while" "(" Expr ")" Stmt ;

ForStmnt.     Stmt ::= "for" "(" LatteType Ident ":" Ident ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- LatteTypes ---------------------------------------------------

IntType.       LatteType ::= "int" ;

StrType.       LatteType ::= "string" ;

BoolType.      LatteType ::= "boolean" ;

VoidType.      LatteType ::= "void" ;

ObjType.       LatteType ::= Ident;

ArrType.       LatteType ::= LatteType "[" "]";

internal   Fun. LatteType ::= LatteType "(" [LatteType] ")" ;

separator  LatteType "," ;

-- Expressions ---------------------------------------------


EVar.      Expr6 ::= Ident;

EAtt.      Expr6 ::= Ident "." Expr;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

ENewArr.   Expr6 ::= "new" LatteType "[" Expr "]" ;

EArr.   Expr6 ::= Ident "[" Expr "]" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Nott.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 7 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;


